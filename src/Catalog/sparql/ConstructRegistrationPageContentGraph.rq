PREFIX nuget: <http://schema.nuget.org/schema#>
PREFIX catalog: <http://schema.nuget.org/catalog#>

CONSTRUCT
{
    ?package a nuget:Package ;
	         nuget:packageContent ?packageContent ;
	         nuget:catalogEntry ?catalogEntry .

    ?catalogEntry nuget:id ?id ;
                  nuget:version ?version ;
	              nuget:published ?published ;
                  nuget:dependencyGroup ?dependency_group ;
                  nuget:listed ?listed ;
                  nuget:description ?description ;
                  nuget:iconUrl ?iconUrl ;
                  nuget:licenseUrl ?licenseUrl ;
                  nuget:projectUrl ?projectUrl ;
                  nuget:requireLicenseAcceptance ?requireLicenseAcceptance ;
                  nuget:authors ?authors ;
                  nuget:tag ?tag .

    ?dependency_group nuget:dependency ?dependency ;
                      nuget:targetFramework ?dependency_group_targetFramework .

    ?dependency nuget:id ?dependency_id ;
                nuget:range ?dependency_range .
}
WHERE
{
	BIND (@catalogEntry AS ?catalogEntry)
	BIND (@package AS ?package)

    ?catalogEntry nuget:version ?version ;
                  nuget:id ?id ;
	              nuget:published ?published .

	BIND(@packageContent AS ?packageContent)

	OPTIONAL { ?catalogEntry nuget:description ?description . }
	OPTIONAL { ?catalogEntry nuget:iconUrl ?iconUrl . }
	OPTIONAL { ?catalogEntry nuget:licenseUrl ?licenseUrl . }
	OPTIONAL { ?catalogEntry nuget:projectUrl ?projectUrl . }
	OPTIONAL { ?catalogEntry nuget:requireLicenseAcceptance ?requireLicenseAcceptance . }
	OPTIONAL { ?catalogEntry nuget:authors ?authors . }
	OPTIONAL { ?catalogEntry nuget:tag ?tag . }

	BIND (IF (year(?published) = 1900, false, true) as ?listed)

    OPTIONAL
    {
        ?catalogEntry nuget:dependencyGroup ?dependency_group .

        OPTIONAL { ?dependency_group nuget:targetFramework ?dependency_group_targetFramework . }

		OPTIONAL
		{
			?dependency_group nuget:dependency ?dependency .
			?dependency nuget:id ?dependency_id .
	        OPTIONAL { ?dependency nuget:range ?dependency_range . }
		}
    }
}
