PREFIX nuget: <http://schema.nuget.org/schema#>

CONSTRUCT
{
	?packageInfo a nuget:PackageInfo ;
	             nuget:package ?package ;
				 nuget:registration ?registration .

    ?package a nuget:Package ;
	         a nuget:PartialEntity ;
			 nuget:partialEntityDescription <http://schema.nuget.org/schema#packageInfoPartialEntityDescription> ;
             nuget:id ?id ;
             nuget:version ?version ;
	         nuget:published ?published ;
             nuget:description ?description ;
             nuget:title ?title ;
             nuget:summary ?summary ;
             nuget:iconUrl ?iconUrl ;
             nuget:licenseUrl ?licenseUrl ;
             nuget:projectUrl ?projectUrl ;
             nuget:requireLicenseAcceptance ?requireLicenseAcceptance ;
             nuget:language ?language ;
             nuget:authors ?authors ;
             nuget:minClientVersion ?minClientVersion .
}
WHERE
{
	BIND (@package AS ?package)

    ?package nuget:id ?id ;
             nuget:version ?version ;
	         nuget:published ?published .

	OPTIONAL { ?package nuget:description ?description . }
	OPTIONAL { ?package nuget:title ?title . }
	OPTIONAL { ?package nuget:summary ?summary . }
	OPTIONAL { ?package nuget:iconUrl ?iconUrl . }
	OPTIONAL { ?package nuget:licenseUrl ?licenseUrl . }
	OPTIONAL { ?package nuget:projectUrl ?projectUrl . }
	OPTIONAL { ?package nuget:requireLicenseAcceptance ?requireLicenseAcceptance . }
	OPTIONAL { ?package nuget:language ?language . }
	OPTIONAL { ?package nuget:authors ?authors . }
	OPTIONAL { ?package nuget:minClientVersion ?minClientVersion . }

    BIND (IRI(CONCAT(@baseAddress, "info/", ?id, "/", ?version, ".json")) AS ?packageInfo)
    BIND (IRI(CONCAT(@registrationBaseAddress, ?id, "/index.json")) AS ?registration)
}
